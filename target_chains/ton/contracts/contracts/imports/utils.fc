;; Built-in assembly functions
int keccak(slice s, int h) asm "HASHEXT_KECCAK256"; ;; Keccak-256 hash function
int keccak_4(slice s1, slice s2, slice s3, slice s4, int h) asm "HASHEXT_KECCAK256";  ;; Keccak-256 hash function for vm body since it spans across 4 slices

int keccak256(slice s) inline {
    return keccak(s, 1);
}

int hash_vm_body(slice s) inline {
    slice s1 = s~load_bits(s.slice_bits());
    s = s~load_ref().begin_parse();
    slice s2 = s~load_bits(s.slice_bits());
    s = s~load_ref().begin_parse();
    slice s3 = s~load_bits(s.slice_bits());
    s = s~load_ref().begin_parse();
    slice s4 = s~load_bits(s.slice_bits());
    int hash = keccak_4(s1, s2, s3, s4, 4);
    slice hash_slice = begin_cell().store_uint(hash, 256).end_cell().begin_parse();
    return keccak256(hash_slice);
}

;; Splits a slice into chunks of 1016 bits or less, in reverse order
(cell, slice) split_into_reverse_chunks(slice data, int size) {
    cell chunks = null();
    int total_bits_loaded = 0;
    builder current_chunk = begin_cell();
    while ((~ data.slice_empty?()) & (total_bits_loaded < size)) {
        int bits_to_load = min(min(data.slice_bits(), 1016 - current_chunk.builder_bits()), size - total_bits_loaded);
        current_chunk = current_chunk.store_slice(data~load_bits(bits_to_load));
        total_bits_loaded += bits_to_load;
        if ((current_chunk.builder_bits() == 1016) | (size - total_bits_loaded == 0)) {
            slice current_chunk_slice = current_chunk.end_cell().begin_parse();
            if (cell_null?(chunks)) {
                chunks = begin_cell().store_slice(current_chunk_slice).end_cell();
            } else {
                chunks = begin_cell().store_slice(current_chunk_slice).store_ref(chunks).end_cell();
            }
            current_chunk = begin_cell();
        }
        if ((data.slice_bits() == 0) & (~ data.slice_refs_empty?())) {
            data = data~load_ref().begin_parse();
        }
    }
    return (chunks, data);
}


(cell, slice) read_and_store_large_data(slice in_msg_body, int size) impure {
    (cell chunks, slice remaining) = split_into_reverse_chunks(in_msg_body, size);
    cell last_cell = null();
    while (~ cell_null?(chunks)) {
        slice chunk = chunks.begin_parse();
        builder cb = begin_cell().store_slice(chunk~load_bits(chunk.slice_bits()));
        if (~ cell_null?(last_cell)) {
            cb = cb.store_ref(last_cell);
        }
        last_cell = cb.end_cell();
        if (chunk.slice_refs_empty?()) {
            chunks = null();
        } else {
            chunks = chunk~load_ref();
        }
    }

    return (last_cell, remaining);
}

(int) parse_pubkey(int x1, int x2) {
    slice pubkey = begin_cell()
            .store_uint(x1, 256)
            .store_uint(x2, 256)
            .end_cell()
            .begin_parse();
    return keccak256(pubkey) & 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF;
}
